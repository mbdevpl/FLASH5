!!****if* source/Grid/GridParticles/GridParticlesMove/gr_ptMovePttoPt
!!
!! NAME
!!  gr_ptMovePttoPt
!!
!! SYNOPSIS
!!
!!  gr_ptMovePttoPt(real(INOUT)    :: dataBuf(:,:),
!!                 integer(INOUT) :: localCount,
!!                 integer(IN)    :: propCount,
!!                 integer(IN)    :: maxCount,
!!                 integer(INOUT) :: numDest,
!!        optiona, integet(IN)    :: matchProp)
!!  
!! DESCRIPTION 
!!  
!!  This routine moves non stationary Grid data elements to the 
!!  correct block and processor. The data elements such as particles 
!!  may have moved because of time advancement, or 
!!  after the grid has refined or derefined. Other data elements such
!!  as those associated with rays follow the  path of the ray.
!!
!!
!!  Overview of algorithm
!!
!!  * Sort the elements in the order of their associated blocks.  This makes for
!!  faster searching and processing of particles
!!
!!  * Find all the elements that belong on one of the local blocks
!!  and make sure that the appropriate block number is associated 
!!  with them.
!!
!!  * send elements in buffer to the first right neighbor. (send a
!!  dummy if no elements need to move off proc), and recv them from
!!  immediate left neighbor and add them to the elements data
!!  structure
!!
!!  * Also send gr_globalID and oldBlksProcessed arrays to neighbor
!!
!!  * repeat this process sending uprocessed elements to immediate
!!  left neighbor, then right neighbor + 2, then left neighbor + 2 and
!!  so on until all the elements have been processed.
!!
!!
!! ARGUMENTS 
!!
!!  dataBuf : List of data elements. 
!!            It is two dimensional real array, the first dimension
!!            represents each particle's properties, and second dimension is index to
!!              elements.
!!
!!  localCount : While coming in it contains the current number of elements mapped to
!!                      this processor. After all the data structure movement, the number
!!                      of local elements might change, and the new value is put back into it
!!  propCount : number of particle attributes
!!  maxCount : This is parameter determined at runtime, and is the maximum number of loca
!!             elements that a simulation expects to have. All the arrays in the elements
!!              unit are allocated based on this number
!!  numDest  : the count of data elements in the sieve at any iteration
!!  matchProp : property to be matched to find the destination of the data element
!!
!! NOTES
!!   
!!
!! SEE ALSO
!!
!!  gr_ptLocalMatch
!!
!!
!!
!!***

#include "constants.h"
#include "Flash.h"

subroutine gr_ptMovePttoPt(dataBuf,propCount,maxCount,localCount, numDest)

  use Timers_interface, ONLY : Timers_start, Timers_stop
  use Grid_data, ONLY : gr_useParticles, gr_meshNumProcs, gr_meshMe, gr_meshComm, &
       gr_useEnergyDeposition
  use gr_ptData, ONLY : gr_ptBlkList,gr_ptBlkCount,&
       gr_ptDestBuf, gr_ptSourceBuf, gr_ptBlk, gr_ptProc
  use ut_sortInterface, ONLY : ut_sortOnProcs
  use Driver_interface, ONLY : Driver_abortFlash
  implicit none

#include "constants.h"
#include "Flash.h"
#include "Flash_mpi.h"

  integer,intent(INOUT) :: localCount
  integer,intent(IN) :: propCount, maxCount, numDest

  real, dimension(propCount, maxCount),intent(INOUT) :: dataBuf


  integer :: blockID,ierr

  integer, dimension(gr_meshNumProcs) :: perProc,toProcs, fromProcs,&
       fromCount,maxNum
  integer, allocatable, dimension(:,:) :: status
  integer, allocatable, dimension(:) :: req
  integer :: sendCount,recvCount, i,j,k
  integer :: countAndProc,bufSize
  integer :: gettingFrom, sendingTo
  integer :: logUnit
  logical, parameter :: logUnitLocal=.true.
  integer, parameter :: tag_for_count=23
  integer, parameter :: tag_for_data=33

  !if we have turned off particles then return


  if(.not. (gr_useParticles .or. gr_useEnergyDeposition)) return


  if(gr_meshNumProcs==1) then
     return
  end if

  fromCount=0
  !! Sort the particles to be moved based upon the processor number of 
  !! their destination
  call ut_sortOnProcs(numDest,propCount, gr_ptProc, gr_meshNumProcs,&
        gr_ptDestBuf,gr_ptSourceBuf, &
       perProc, toProcs,sendingTo)

  !! share that information with all other processors 
  call MPI_Allreduce(toProcs,fromProcs,gr_meshNumProcs,FLASH_INTEGER,MPI_SUM,gr_meshComm,ierr)
  !! At this point all the processors know how many processors they should
  !! expect to get the data from. That information is stored in fromProcs
  !! the +1 is there because proc numbers start from zero.
  
  !! The communication itself happens in two steps. In the first each
  !! processor gets the number of particles to expect from all the processor
  !! that are sending to it, and in the next step the actual transmission of
  !! particles takes place



  gettingFrom=fromProcs(gr_meshMe+1)

  allocate(status(MPI_STATUS_SIZE,gettingFrom))
  allocate(req(gettingFrom))
  req(:)=0
  status=0
  recvCount = 1

#ifdef GLOBAL
  call MPI_ALLReduce(perProc,maxNum, gr_meshNumProcs, FLASH_INTEGER,MPI_MAX,gr_meshComm,ierr)
 recvCount=maxNum(gr_meshMe+1)
#else
  !! Post all non-blocking receives necessary
  if(gettingFrom>0) then
     do i = 1,gettingFrom
        call MPI_Irecv(fromProcs(i),recvCount,FLASH_INTEGER,MPI_ANY_SOURCE,&
             tag_for_count,gr_meshComm,req(i),ierr)
     end do
  end if

  !! schedule all sends in blocking mode
  if(sendingTo>0) then
     j=0
     do i=1,sendingTo
        do while(perProc(j+1)==0)
           j=j+1
        end do
        
        countAndProc=perProc(j+1)
        sendCount=1
        call MPI_Send(countAndProc,sendCount,FLASH_INTEGER,j,tag_for_count,gr_meshComm,ierr)
        j=j+1
        
     end do

  end if

  !! This is where we wait for the all the posted receives to be done,
  !! find the maximum number of particles mype is expected to receive
  !! and allocate a buffer large enough to receive it.

  if(gettingFrom>0) then
     call MPI_Waitall(gettingFrom,req,status,ierr)
     recvCount=(maxval(fromProcs(1:gettingFrom)))
  end if
#endif
  if(gettingFrom>0) then
     if ((gettingFrom*recvCount) > ubound(gr_ptSourceBuf,2)) then
        print *, "Overflow! Mesh PE", gr_meshMe, &
             ", gettingFrom", gettingFrom, &
             ", recvCount", recvCount, &
             ", max particles", ubound(gr_ptSourceBuf,2)
        call Driver_abortFlash("[gr_ptMovePttoPt]: Insufficient space "//&
             "in particles communication buffer: "//&
             "increase pt_maxPerProc if you are using the Particles unit "//&
             "or ed_maxRayCount if you are using the EnergyDeposition unit.")
     end if
     gr_ptSourceBuf(:,:)=NONEXISTENT
     bufSize = recvCount*propCount
     
     req(:)=0
     status=0
     j=1

     !! Here the receives are posted to get the particles
     do i =1,gettingFrom
        call MPI_Irecv(gr_ptSourceBuf(1,j),bufSize,&
             FLASH_REAL,MPI_ANY_SOURCE,tag_for_data,gr_meshComm,&
             req(i),ierr)
        j=j+recvCount
     end do
     
  end if

  if(sendingTo>0) then
     j=0
     k=1
     
     do i=1,sendingTo
        do while(perProc(j+1)==0)
           j=j+1
        end do
        sendCount=perProc(j+1)
        bufSize=sendCount*propCount
        call MPI_Send(gr_ptDestBuf(1,k),bufSize,FLASH_REAL,j,tag_for_data,gr_meshComm,ierr)
        j=j+1
        k=k+sendCount
        
     end do
  end if
  

  if(gettingFrom>0) then
     call MPI_Waitall(gettingFrom,req,status,ierr)
     do i = 1,recvCount*gettingFrom
        
        blockID=int(gr_ptSourceBuf(gr_ptBlk,i))
        if(blockID/=NONEXISTENT) then
           localCount=localCount+1
           if (localCount > maxCount) then
              call Driver_abortFlash("[gr_ptMovePttoPt]: Insufficient space "//&
                   "in particles communication buffer: "//&
                   "increase pt_maxPerProc if you are using the Particles unit "//&
                   "or ed_maxRayCount if you are using the EnergyDeposition unit.")
           end if
           databuf(:,localCount)=gr_ptSourceBuf(:,i)
        end if
     end do
     deallocate(req)
     deallocate(status)
  end if


end subroutine gr_ptMovePttoPt


