!!****if* source/Driver/DriverMain/Driver_initFlash
!!
!! NAME
!!  Driver_initFlash
!!
!! SYNOPSIS
!!
!!   Driver_initFlash()
!!
!! DESCRIPTION
!!
!!  Performs Flash initializations, which includes:
!!
!!  Call all 'init' routines in units.  Order does matter,
!!  particularly when restarting from a checkpoint file.
!!
!!  For the most part, Driver_initFlash calls other units' init
!!  routines directly, like call IO_init or call Grid_init.  This
!!  routine also makes calls to other Driver initialization routines
!!  like Driver_initMaterialProperties or Driver_initSourceTerms.
!!  These routines then call the unit-specific initialization 
!!  routines.  This level of abstraction was added to simplify
!!  the initialization calls.
!!
!! NOTES
!!
!! The Driver unit uses a few unit scope variables that are
!! accessible to all routines within the unit, but not to the
!! routines outside the unit. These variables begin with "dr_"
!! like, dr_myPE or dr_dt, dr_beginStep, and are stored in fortran
!! module Driver_data (in file Driver_data.F90. The other variables
!! are local to the specific routine and do not have the prefix "dr_"
!!
!! HISTORY 
!!
!!  2008-03-14 KW   Moved material properties initialization up.
!!
!!***


subroutine Driver_initFlash()
  
  use Driver_data, ONLY: dr_myPE, dr_numProcs, dr_nbegin, &
       dr_initialSimTime, dr_elapsedWCTime, &
       dr_initialWCTime, dr_restart, dr_dtInit, dr_redshift,dr_particlesInitialized

  use Driver_interface, ONLY : Driver_initParallel, Driver_init, &
    Driver_initMaterialProperties, Driver_initSourceTerms, &
    Driver_verifyInitDt, Driver_abortFlash
  use RuntimeParameters_interface, ONLY : RuntimeParameters_init, RuntimeParameters_get
  use Logfile_interface, ONLY : Logfile_init
  use Flame_interface, ONLY : Flame_init
  use PhysicalConstants_interface, ONLY : PhysicalConstants_init
  use Gravity_interface, ONLY : Gravity_init, &
    Gravity_potentialListOfBlocks
  use Timers_interface, ONLY : Timers_init, Timers_start, Timers_stop

  use Grid_interface, ONLY : Grid_init, Grid_initDomain, &
    Grid_getListOfBlocks, Grid_dump
  use Multispecies_interface, ONLY : Multispecies_init
  use Particles_interface, ONLY : Particles_init,  Particles_initData, &
       Particles_initForces
 
  use Eos_interface, ONLY : Eos_init
  use Hydro_interface, ONLY : Hydro_init
  use Simulation_interface, ONLY : Simulation_init
  use Cosmology_interface, ONLY : Cosmology_init
  use IO_interface, ONLY :IO_init, IO_outputInitial
  use Gravity_interface, ONLY :  Gravity_potentialListOfBlocks
  implicit none       
  
  include 'Flash_mpi.h'
  
#include "constants.h"
#include "Flash.h"

  integer :: blockCount, lb
  integer :: blockList(MAXBLOCKS)
  logical :: updateRefine

  dr_elapsedWCTime = 0.0

  !! Initialize message-passing interface and mesh package.
  !! This step and the timer initialization must not depend
  !! on any runtime parameters, because they (the parameters)
  !! have not yet been initialized.
  call Driver_initParallel(dr_myPE, dr_numProcs)

  !! hand myPE out to C routines to avoid architecture-dependent code
  call driver_abortflashc_set_mype(dr_myPE)

  !! make sure our stack (and whatever other rlimits) are big enough.  
  !! this should get around the 2Mb stack limit that pthreads
  !! imposes if linked statically (but not dynamically!)
  call dr_set_rlimits(dr_myPE)

    
  !! Initialize runtime parameters
  call RuntimeParameters_init(dr_myPE, dr_restart)

  !! Initialize the code timers.  Ideally should be first thing in
  !! code but currently the timing package
  !! uses MPI_WTime(), so Driver_initParallel() must go first, and
  !! uses RuntimeParameters_get(), so RuntimeParameters_init() must go
  !! first.
  call Timers_init(dr_myPE, dr_numProcs, dr_initialWCTime)
  call Timers_start("initialization")


  !PhysicalConstants init and Multispecies init must come before Logfile
  !since their values are stamped to the logfile
  call PhysicalConstants_init(dr_myPE, dr_numProcs)

  !must come before EOS
  call Multispecies_init(dr_myPE, dr_numProcs)

  call Logfile_init(dr_myPE, dr_numProcs)

  call Grid_init(dr_myPE, dr_numProcs)
  
  call Driver_initMaterialProperties(dr_myPE)
  if(dr_myPE==MASTER_PE)print*,'MaterialProperties initialized'
  
  call Flame_init(dr_myPE)

  call RuntimeParameters_get('dtInit',dr_dtInit)

  call Particles_init(dr_myPE, dr_numProcs, dr_restart)       ! Particles
  
#ifdef DEBUG_DRIVER
  if(dr_myPE==MASTER_PE)print*,'Particles initialized'
#endif

  if(.not. dr_restart) then     
     
     call Cosmology_init(dr_myPE, dr_restart)
     if(dr_myPE==MASTER_PE)print*,'Cosmology initialized'

     call Driver_init(dr_myPE)

     !Eos must come before Grid
     call Eos_init(dr_myPE)

     call Driver_initSourceTerms(dr_myPE, dr_restart)
     if(dr_myPE==MASTER_PE)print*,'Source terms initialized'

     !must come before Grid since simulation specific values must go on the Grid

     call Simulation_init(dr_myPE)


     call Grid_initDomain(dr_myPE, dr_numProcs,dr_restart,dr_particlesInitialized)
     if (dr_myPE==MASTER_PE)print *, ' Finished with Grid_initDomain, no restart'

     call IO_init(dr_myPE, dr_numProcs)

  else if(dr_restart) then
     
     call IO_init(dr_myPE, dr_numProcs)

     call Cosmology_init(dr_myPE, dr_restart)

     call Driver_init(dr_myPE)

     call Eos_init(dr_myPE)

     call Driver_initSourceTerms(dr_myPE, dr_restart)
     if(dr_myPE==MASTER_PE)print*,'Source terms initialized'

     call Simulation_init(dr_myPE)
     dr_particlesInitialized=.true.
     call Grid_initDomain(dr_myPE, dr_numProcs, dr_restart,dr_particlesInitialized)
     if (dr_myPE==MASTER_PE) print *, ' Finished with Grid_initDomain, restart'

     call Grid_getListOfBlocks(LEAF,blockList,blockCount)
     do lb = 1, blockCount
        call Grid_dump((/DENS_VAR/),1,blockList(lb),gcell=.FALSE.)
        call Grid_dump((/PRES_VAR/),1,blockList(lb),gcell=.FALSE.)
        call Grid_dump((/TEMP_VAR/),1,blockList(lb),gcell=.FALSE.)
        call Grid_dump((/TION_VAR/),1,blockList(lb),gcell=.FALSE.)
        call Grid_dump((/TELE_VAR/),1,blockList(lb),gcell=.FALSE.)
        call Grid_dump((/TRAD_VAR/),1,blockList(lb),gcell=.FALSE.)
     end do
     STOP
  end if

  !Hydro_init must go before Driver
  if(dr_myPE==MASTER_PE) print *, 'Ready to call Hydro_init' 
  call Hydro_init(dr_myPE)           ! Hydrodynamics, MHD, RHD
  if(dr_myPE==MASTER_PE)print*,'Hydro initialized'
  
  
  call Gravity_init(dr_myPE)         ! Gravity
  if(dr_myPE==MASTER_PE)print*,'Gravity initialized'


  call Driver_verifyInitDt(dr_myPE)
  if(dr_myPE==MASTER_PE)print*,'Initial dt verified'
 
  !For active particle simulations we must initialize particle 
  !positions before the call to Gravity_potentialListOfBlocks.
  call Particles_initData(dr_restart,dr_particlesInitialized)
  
  if(.not. dr_restart) then
     call Grid_getListOfBlocks(LEAF,blockList,blockCount)
     call Gravity_potentialListOfBlocks(blockCount,blockList)
     call Particles_initForces
  end if

  call IO_outputInitial(dr_myPE, dr_numProcs, dr_nbegin, dr_initialSimTime)
  if(dr_myPE==MASTER_PE)print*,'Initial plotfile written'

  if(dr_myPE==MASTER_PE)print*,'Driver init all done'

  !!Done with initialization.
  call Timers_stop ("initialization")


  return
end subroutine Driver_initFlash
