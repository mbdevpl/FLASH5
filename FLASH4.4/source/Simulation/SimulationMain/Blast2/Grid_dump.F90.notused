!!****if* source/Simulation/SimulationMain/PhoenixInputKeepNames/Grid_dump
!!
!! NAME
!!  Grid_dump
!!
!! SYNOPSIS
!!
!!  call Grid_dump(integer(IN) :: var(num),
!!                 integer(IN) :: num,
!!                 integer(IN) :: blockID,
!!                 logical(IN) :: gcell)
!!
!! DESCRIPTION 
!!  
!! Dumps the variables specified in "var" to a file. Can be done from 
!! anywhere in the code, and is useful for diagnostic purposes
!! With paramesh this function doesn not work in parallel, but works
!! only with a single block
!!  
!! ARGUMENTS 
!!
!!  var :: array containing the indices of the variables to be dumped
!!  num :: number of variables being dumped.
!!  blockID :: local number of block to be dumped
!!  gcell :: indicates whether to include guardcells in the dump.
!!             
!! EXAMPLE
!!  
!!  num = 3  !dumping 3 variables
!!  var(1) = DENS_VAR
!!  var(2) = PRES_VAR
!!  var(3) = TEMP_VAR
!!  blockID = 1  ! local block number
!!  gcell = .false.
!!
!!  call Grid_dump(var, num, blockID, gcell)
!!  
!!  will dump the interior cell values of density, pressure and temperature
!!  for local block number 1.
!!
!! NOTES
!!  DENS_VAR, PRES_VAR, TEMP_VAR etc are #defined values in Flash.h
!!  indicating the index in the physical data array.
!!  The routine calling Grid_dump will need to include Flash.h 
!! 
!!  This routine works with Paramesh only under very special circumstances
!!***

#include "constants.h"
#include "Flash.h"

subroutine Grid_dump(var,num, blockID, gcell)

  use Grid_data, ONLY : gr_ilo, gr_ihi, gr_jlo, gr_jhi, &
       gr_klo, gr_khi, gr_iloGC, gr_ihiGC, gr_jloGC, gr_jhiGC, &
       gr_kloGC, gr_khiGC
  use Grid_interface, ONLY: Grid_getCellCoords

  use physicaldata, ONLY :unk

#include "Flash.h"

  implicit none

  integer, intent(IN) :: num, blockID
  integer, dimension(num), intent(IN) :: var
  logical, intent(IN) :: gcell
  integer,save :: fu(NUNK_VARS) = -1
!  data ff / (NUNK_VARS) * (-1) / 
  integer,save :: lastOpenedUnit = 40

  character(len=80) :: ff1
  integer,dimension(4), save :: filecount = 0
  integer :: n,i,j,k,ivar

  integer,parameter :: bxn=GRID_IHI_GC-GRID_ILO_GC+1
  integer,parameter :: byn=GRID_JHI_GC-GRID_JLO_GC+1 
  integer,parameter :: bzn=GRID_KHI_GC-GRID_KLO_GC+1 
  real,dimension(1,bxn,byn,bzn,1)::scratch
  real :: x(1:GRID_IHI_GC)
  real :: y(1:GRID_JHI_GC)
  real :: z(1:GRID_KHI_GC)

!!$  if(.not. gcell) then
!!$     print '(8F11.6)', unk(var(1), gr_ilo:gr_ihi, gr_jlo:gr_jhi, gr_klo:gr_khi, blockID)
!!$
!!$  else
!!$     print '(16F7.3)', unk(var(1), gr_iloGc:gr_ihiGc, gr_jloGc:gr_jhiGc, gr_kloGc:gr_khiGc, blockID)
!!$  end if


  call Grid_getCellCoords(IAXIS,blockID,CENTER,.TRUE.,x,GRID_IHI_GC)
  call Grid_getCellCoords(JAXIS,blockID,CENTER,.TRUE.,y,GRID_JHI_GC)
  call Grid_getCellCoords(KAXIS,blockID,CENTER,.TRUE.,z,GRID_KHI_GC)

22 format('UNK',I4.4,'.dump')

  do n=1,num
     ivar = var(n)
     IF (fu(ivar) < 0) then
        lastOpenedUnit = lastOpenedUnit + 1
        fu(ivar) = lastOpenedUnit
        write(ff1,22) ivar
        open(fu(ivar),file=ff1,form='formatted',status='UNKNOWN')
     end if
     do k=GRID_KLO,GRID_KHI
        do j=GRID_JLO,GRID_JHI
           do i=GRID_ILO,GRID_IHI
999           format(4(1x,1PG15.4),0P,5(1x,I6))
              write(FU(IVAR),999) x(i),y(j),z(k),unk(ivar,i,j,k,blockID),blockID,i,j,k,ivar
           end do
        end do
     end do
  end do

  !print '(8F11.6)', unk(var(1), gr_ilo:gr_ihi, gr_jlo:gr_jhi, gr_klo:gr_khi, blockID) 

  return
end subroutine Grid_dump
